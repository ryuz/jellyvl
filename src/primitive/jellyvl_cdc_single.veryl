module cdc_single #(
    param DEST_SYNC_FF  : i32    = 4      ,
    param SIM_ASSERT_CHK: i32    = 0      ,
    param SRC_INPUT_REG : bit    = 1      ,
    param DEVICE        : string = "RTL"  ,
    param SIMULATION    : string = "false",
    param DEBUG         : string = "false",
) (
    src_clk : input  'src clock,
    src_in  : input  'src logic,
    dest_clk: input  'dst clock,
    dest_out: output 'dst logic,
) {

    if (DEVICE == "SPARTAN6" || DEVICE == "VIRTEX6" || DEVICE == "7SERIES" || DEVICE == "ULTRASCALE" || DEVICE == "ULTRASCALE_PLUS" || DEVICE == "ULTRASCALE_PLUS_ES1" || DEVICE == "ULTRASCALE_PLUS_ES2" || DEVICE == "VERSAL_AI_CORE" || DEVICE == "VERSAL_AI_CORE_ES1" || DEVICE == "VERSAL_AI_CORE_ES2" || DEVICE == "VERSAL_PRIME" || DEVICE == "VERSAL_PRIME_ES1" || DEVICE == "VERSAL_PRIME_ES2") :xilinx {
        unsafe (cdc) {
            inst u_xpm_cdc_single: $sv::xpm_cdc_single #(
                DEST_SYNC_FF  : DEST_SYNC_FF  ,
                SIM_ASSERT_CHK: SIM_ASSERT_CHK,
                SRC_INPUT_REG : SRC_INPUT_REG ,
            ) (
                src_clk   ,
                src_in    ,
                dest_clk  ,
                dest_out  ,
            );
        }
    } else :rtl {
        var src_in_reg: 'src logic;
        always_ff (src_clk) {
            src_in_reg = src_in;
        }

        #[sv("ASYNC_REG = \"TRUE\"")]
        var dest_out_reg: 'dst logic [DEST_SYNC_FF];

        unsafe (cdc) {
            always_ff (dest_clk) {
                dest_out_reg[0] = if SRC_INPUT_REG ? src_in_reg : src_in;
                for i: u32 in 1..DEST_SYNC_FF {
                    dest_out_reg[i] = dest_out_reg[i - 1];
                }
            }
        }

        assign dest_out = dest_out_reg[DEST_SYNC_FF - 1];
    }
}
